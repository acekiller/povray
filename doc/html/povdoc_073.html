<!------------------------------------------------------------------------------->
<!--              Generated by makedocs.pl version 1.0 rev 20                  -->
<!--              This file Copyright (c) POV-Team 1991-2002                   -->
<!-- You can override our stylesheet settings by placing a 'povray35.css' file -->
<!--    in the root directory of the disk volume that holds this help file.    -->
<!------------------------------------------------------------------------------->
<html>
<head>
<title>Bicubic Patch Object</title>
<link rel="stylesheet" type="text/css" href="../povray35.css">
<link rel="stylesheet" type="text/css" href="file:///povray35.css">
<object id=htmlhelp type="application/x-oleobject" classid="clsid:adb880a6-d8ff-11cf-9377-00aa003b7a11"></object>
<script language=javascript src="../povray35.js"></script>
</head>
<a name="target_121"></a>
<body>
<center>
<table width=100% border=0>
<tr>
<td>
<table width=100% align="center" border=0 bgcolor="#ffcc99" class="NavBar">
<tr>
<td align="left">&lt;<a href="povdoc_072.html">Prev</a></td>
<script language="JavaScript">
<!--
if (navigator.userAgent.indexOf ("MSIE 3.") >= 1)
  document.write ("<td align=center><small><i>You are running <b>IE3</b>. This will likely cause problems with this help file.</i></small></td>") ;
else if (navigator.userAgent.indexOf ("MSIE 4.") >= 1)
  document.write ("<td align=center><small><i>The Microsoft HTML Help system works best with <b>IE5</b> or later. We recommend you upgrade.</i></small></td>") ;
//-->
</script>
<td align="right"><a href="povdoc_074.html">Next</a>&gt;</td></tr>
</table>
</td>
</tr>
</table>
</center>
<div class="divh3">
<h3>3.4.5&nbsp;&nbsp;Bicubic Patch Object</h3>
<a name="target_122"></a>

<p>
Bicubic patches are useful surface representations because they allow an easy definition
 of surfaces using only a few control points. The control points serve to determine the shape
 of the patch. Instead of defining the vertices of triangles, we simply give the coordinates
 of the control points. A single patch has 16 control points, one at each corner, and the rest
 positioned to divide the patch into smaller sections. POV-Ray does not ray trace the patches
 directly, they are approximated using triangles as decribed in the
 <a href="povdoc_188.html#target_830">Scene Description Language</a> section.
</p>
<p>
Bicubic patches are almost always created by using a third party modeler, but for this tutorial
 we will manipulate them by hand. Modelers that support Bicubic patches and export to POV-Ray
 can be found in the Link section at <a href="http://povray.org/links/" target="new" class="ExternalLink">»www.povray.org</a><br>
Let's set up a basic scene and start exploring the Bicubic patch.
</p>
<pre>
#version 3.5;
global_settings {assumed_gamma 1.0}
background {rgb &lt;1,0.9,0.9&gt;}
camera {location &lt;1.6,5,-6&gt; look_at &lt;1.5,0,1.5&gt; angle 40}
light_source {&lt;500,500,-500&gt; rgb 1 }

#declare B11=&lt;0,0,3&gt;; #declare B12=&lt;1,0,3&gt;; //
#declare B13=&lt;2,0,3&gt;; #declare B14=&lt;3,0,3&gt;; // row 1

#declare B21=&lt;0,0,2&gt;; #declare B22=&lt;1,0,2&gt;; //
#declare B23=&lt;2,0,2&gt;; #declare B24=&lt;3,0,2&gt;; // row 2

#declare B31=&lt;0,0,1&gt;; #declare B32=&lt;1,0,1&gt;; //
#declare B33=&lt;2,0,1&gt;; #declare B34=&lt;3,0,1&gt;; // row 3

#declare B41=&lt;0,0,0&gt;; #declare B42=&lt;1,0,0&gt;; //
#declare B43=&lt;2,0,0&gt;; #declare B44=&lt;3,0,0&gt;; // row 4

bicubic_patch {
   type 1 flatness 0.001
   u_steps 4 v_steps 4
   uv_vectors
   &lt;0,0&gt; &lt;1,0&gt; &lt;1,1&gt; &lt;0,1&gt;
   B11, B12, B13, B14
   B21, B22, B23, B24
   B31, B32, B33, B34
   B41, B42, B43, B44
   uv_mapping
   texture {
      pigment {
         checker 
         color rgbf &lt;1,1,1,0.5&gt; 
         color rgbf &lt;0,0,1,0.7&gt; 
         scale 1/3
      }
      finish {phong 0.6 phong_size 20}
   }
   no_shadow
}
</pre>
<p>
The points B11, B14, B41, B44 are the corner points of the patch.
 All other points are control points. The names of the declared points are as follows:
 B for the colour of the patch, the first digit gives the row number, the second digit
 the column number. If you render the above scene, you'll get a blue &amp; white
 checkered square, not very exciting. First we will add some spheres to make the control
 points visible. As we don't want to type the code for 16 spheres, we'll use
 an array and a while loop to construct the spheres.
</p>
<pre>
#declare Points=array[16]{
   B11, B12, B13, B14
   B21, B22, B23, B24
   B31, B32, B33, B34
   B41, B42, B43, B44
}
#declare I=0;
#while (I&lt;16)
   sphere {
      Points[I],0.1 
      no_shadow 
      pigment{
         #if (I=0|I=3|I=12|I=15)
            color rgb &lt;1,0,0&gt;
         #else
            color rgb &lt;0,1,1&gt;
         #end
      }
   }
   #declare I=I+1;
#end
</pre>
<p>
Rendering this scene will show the patch with its corner points in red and its control
 points in cyan. Now it's time to start exploring.
<br>
Change B41 to <code>&lt;-1,0,0&gt;</code> and render.<br>
Change B41 to <code>&lt;-1,1,0&gt;</code> and render.<br>
Change B41 to <code>&lt; 1,2,1&gt;</code> and render.<br>
</p>
<p>
Let's do some exercise with the control points. Start with a flat patch again.<br>
Change B42 to <code>&lt;1,2,0&gt;</code> and B43 to <code>&lt;2,-2,0&gt;</code> and render.<br>
Change B42 and B43 back to their original positions and try B34 to <code>&lt;4,2,1&gt;</code>  
and B24 to <code>&lt;2,-2,2&gt;</code> and render. Move the points around some more, also
 try the control points in the middle.
</p>
  
<center><img src="images/bpatch01.gif" alt="Bicubic_patch with control points"><br><font size=-1><cite>Bicubic_patch with control points</cite></font></center>

<p>
After all this we notice two things: 
<ul type="disc">
	<li> The patch always goes through the corner points.</li>
	<li> In most situations the patch does not go through the control points. </li>
</ul>
Now go back to our spline work shop and have a look at the bezier_spline again. Indeed,
 the points B11,B12,B13,B14, make up a bezier_spline. So do the points B11,B21,B31,B41 
and B41,B42,B43,B44 and B14,B24,B34,B44.
</p>
<p>
So far we've only been looking at one single patch, but one of the strengths of the
 Bicubic patch lays in the fact that they can be connected smoothly, to form bigger shapes.
 The process of connecting is relatively simple as there are actually only two rules to
 follow. It can be done by using a well set up set of macros or by using a modeler. To give
 an idea what is needed we'll do a simple example by hand.
</p>
<p>
First put the patch in our scene back to its flat position. Then 
change<code> #declare B14=&lt;3,0,3&gt;; #declare B24=&lt;3,2,2&gt;; #declare B34=&lt;3.5,1,1&gt;
; #declare B44=&lt;3,-1,0&gt;; #declare B41=&lt;0,-1,0&gt;;</code> Move the camera a bit back
 <code>camera {location &lt;3.1,7,-8&gt; look_at &lt;3,-2,1.5&gt; angle 40}</code> and delete
 all the code for the spheres. We will now try and stitch a patch to the right side of the
 current one. Off course the points on the left side (column 1) of the new patch have to be
 in the same position as the points on the right side (column 4) of the blue one.<br>
Render the scene, including our new patch:
</p>
<pre>
#declare R11=B14; #declare R12=&lt;4,0,3&gt;;     //
#declare R13=&lt;5,0,3&gt;; #declare R14=&lt;6,0,3&gt;; // row 1

#declare R21=B24; #declare R22=&lt;4,0,2&gt;;     //
#declare R23=&lt;5,0,2&gt;; #declare R24=&lt;6,0,2&gt;; // row 2

#declare R31=B34; #declare R32=&lt;4,0,1&gt;;     //
#declare R33=&lt;5,0,1&gt;; #declare R34=&lt;6,0,1&gt;; // row 3

#declare R41=B44; #declare R42=&lt;4,0,0&gt;;     //
#declare R43=&lt;5,0,0&gt;; #declare R44=&lt;6,0,0&gt;; // row 4

bicubic_patch {
   type 1 flatness 0.001
   u_steps 4 v_steps 4
   uv_vectors
   &lt;0,0&gt; &lt;1,0&gt; &lt;1,1&gt; &lt;0,1&gt;
   R11, R12, R13, R14
   R21, R22, R23, R24
   R31, R32, R33, R34
   R41, R42, R43, R44
   uv_mapping
   texture {
      pigment {
         checker 
         color rgbf &lt;1,1,1,0.5&gt; 
         color rgbf &lt;1,0,0,0.7&gt; 
         scale 1/3
      }
      finish {phong 0.6 phong_size 20}
   }
   no_shadow
}
</pre>
<p>
This is a rather disappointing result. The patches are connected, but not exactly smooth.
 In connecting patches the same principles apply as for connecting two 2D-bezier splines
 as we see in the <a href="povdoc_069.html#target_113">spline workshop</a>.
 Control point, connection point and the next control point should be on one line to give
 a smooth result. Also it is prefered, not required, that the distances from both control
 points to the connection point are the same. For the Bicubic patch we have to do the same,
 for all connection points involved in the joint. So, in our case, the following points 
should be on one line:<br>
<ul type="disc">
	<li> B13, B14=R11, R12</li>
	<li> B23, B24=R21, R22</li>
	<li> B33, B34=R31, R32</li>
	<li> B43, B44=R41, R42</li>
</ul>
To achieve this we do:
</p>
<pre>
#declare R12=B14+(B14-B13); 
#declare R22=B24+(B24-B23); 
#declare R32=B34+(B34-B33); 
#declare R42=B44+(B44-B43); 
</pre>
  
<center><img src="images/bpatch02.gif" alt="patches, (un)smoothly connected"><br><font size=-1><cite>patches, (un)smoothly connected</cite></font></center>
 
<p>
This renders a smooth surface. Adding a third patch in front is relative simple now:</p>
<pre>
#declare G11=B41;      #declare G12=B42;                //
#declare G13=B43;      #declare G14=B44;                // row 1

#declare G21=B41+(B41-B31); #declare G22=B42+(B42-B32); //
#declare G23=B43+(B43-B33); #declare G24=B44+(B44-B34); // row 2

#declare G31=&lt;0,0,-2&gt;; #declare G32=&lt;1,0,-2&gt;;           //
#declare G33=&lt;2,0,-2&gt;; #declare G34=&lt;3,2,-2&gt;;           // row 3

#declare G41=&lt;0,0,-3&gt;; #declare G42=&lt;1,0,-3&gt;;           // 
#declare G43=&lt;2,0,-3&gt;; #declare G44=&lt;3,0,-3&gt;            // row 4

bicubic_patch {
   type 1 flatness 0.001
   u_steps 4 v_steps 4
   uv_vectors
   &lt;0,0&gt; &lt;1,0&gt; &lt;1,1&gt; &lt;0,1&gt;
   G11, G12, G13, G14
   G21, G22, G23, G24
   G31, G32, G33, G34
   G41, G42, G43, G44
   uv_mapping
   texture {
      pigment {
         checker 
         color rgbf &lt;1,1,1,0.5&gt; 
         color rgbf &lt;0,1,0,0.7&gt; 
         scale 1/3
      }
      finish {phong 0.6 phong_size 20}
   }
   no_shadow
}
</pre>
<p>
Finally, let's put a few spheres back in the scene and add some cylinders to visualize what
 is going on. See what happens if you move for example B44, B43, B33 or B34.
</p>
<pre>
#declare Points=array[8]{B33,B34,R32,B43,B44,R42,G23,G24}
#declare I=0;
#while (I&lt;8)
   sphere {
      Points[I],0.1 
      no_shadow 
      pigment{
         #if (I=4)
            color rgb &lt;1,0,0&gt;
         #else
            color rgb &lt;0,1,1&gt;
         #end
      }
   }
   #declare I=I+1;
#end
union {
   cylinder {B33,B34,0.04} cylinder {B34,R32,0.04}
   cylinder {B43,B44,0.04} cylinder {B44,R42,0.04}
   cylinder {G23,G24,0.04} 
   cylinder {B33,B43,0.04} cylinder {B43,G23,0.04}
   cylinder {B34,B44,0.04} cylinder {B44,G24,0.04}
   cylinder {R32,R42,0.04}
   no_shadow 
   pigment {color rgb &lt;1,1,0&gt;}
}
</pre>
<p>
The hard part in using the Bicubic patch isn't in connecting several patches. The 
difficulty is keeping control over the shape you want to build. As patches are added, 
in order to keep the result smooth, control over the position of many points gets restrained.
</p>
   
<center><img src="images/bpatch03.gif" alt="3 patches, some control points"><br><font size=-1><cite>3 patches, some control points</cite></font></center>
 


</div>

<p>
<center>
<table width=100% border=0>
<tr>
<td>
<table width=100% align="center" border=0 bgcolor="#ffcc99" class="NavBar">
<tr>
<td align="left">&lt;<a href="povdoc_072.html">Prev</a></td>
<script language="JavaScript">
<!--
if (navigator.userAgent.indexOf ("MSIE 3.") >= 1)
  document.write ("<td align=center><small><i>You are running <b>IE3</b>. This will likely cause problems with this help file.</i></small></td>") ;
else if (navigator.userAgent.indexOf ("MSIE 4.") >= 1)
  document.write ("<td align=center><small><i>The Microsoft HTML Help system works best with <b>IE5</b> or later. We recommend you upgrade.</i></small></td>") ;
//-->
</script>
<td align="right"><a href="povdoc_074.html">Next</a>&gt;</td></tr>
</table>
</td>
</tr>
</table>
</center>
</body>
</html>
