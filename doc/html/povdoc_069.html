<!------------------------------------------------------------------------------->
<!--              Generated by makedocs.pl version 1.0 rev 20                  -->
<!--              This file Copyright (c) POV-Team 1991-2002                   -->
<!-- You can override our stylesheet settings by placing a 'povray35.css' file -->
<!--    in the root directory of the disk volume that holds this help file.    -->
<!------------------------------------------------------------------------------->
<html>
<head>
<title>Lathe Object</title>
<link rel="stylesheet" type="text/css" href="../povray35.css">
<link rel="stylesheet" type="text/css" href="file:///povray35.css">
<object id=htmlhelp type="application/x-oleobject" classid="clsid:adb880a6-d8ff-11cf-9377-00aa003b7a11"></object>
<script language=javascript src="../povray35.js"></script>
</head>
<a name="target_111"></a>
<body>
<center>
<table width=100% border=0>
<tr>
<td>
<table width=100% align="center" border=0 bgcolor="#ffcc99" class="NavBar">
<tr>
<td align="left">&lt;<a href="povdoc_068.html">Prev</a></td>
<script language="JavaScript">
<!--
if (navigator.userAgent.indexOf ("MSIE 3.") >= 1)
  document.write ("<td align=center><small><i>You are running <b>IE3</b>. This will likely cause problems with this help file.</i></small></td>") ;
else if (navigator.userAgent.indexOf ("MSIE 4.") >= 1)
  document.write ("<td align=center><small><i>The Microsoft HTML Help system works best with <b>IE5</b> or later. We recommend you upgrade.</i></small></td>") ;
//-->
</script>
<td align="right"><a href="povdoc_070.html">Next</a>&gt;</td></tr>
</table>
</td>
</tr>
</table>
</center>
<div class="divh3">
<h3>3.4.1&nbsp;&nbsp;Lathe Object</h3>
<a name="target_112"></a>

  <p>In the real world, <code><a href="povdoc_187.html#target_800">lathe</a></code> refers to a process of making
  patterned rounded shapes by spinning the source material in place and carving
  pieces out as it turns. The results can be elaborate, smoothly rounded,
  elegant looking artefacts such as table legs, pottery, etc. In POV-Ray, a
  lathe object is used for creating much the same kind of items, although we
  are referring to the object itself rather than the means of production.</p><p>
   Here is some source for a really basic lathe.</p>
<pre>
  #include &quot;colors.inc&quot;
  background{White}
  camera {
    angle 10
    location &lt;1, 9, -50&gt;
    look_at &lt;0, 2, 0&gt;
  }
  light_source {
    &lt;20, 20, -20&gt; color White
  }
  lathe {
    linear_spline
    6,
    &lt;0,0&gt;, &lt;1,1&gt;, &lt;3,2&gt;, &lt;2,3&gt;, &lt;2,4&gt;, &lt;0,4&gt;
    pigment { Blue }
    finish {
      ambient .3
      phong .75
    }
  }
</pre>

  
<center><img src="images/latheobj.gif" alt="A simple lathe object"><br><font size=-1><cite>A simple lathe object</cite></font></center>


  <p>We render this, and what we see is a fairly simply type of lathe, which
  looks like a child's top. Let's take a look at how this code produced
  the effect.</p><p>
   First, a set of six points is declared which the raytracer connects with
  lines. We note that there are only two components in the vectors which
  describe these points. The lines that are drawn are assumed to be in the
  x-y-plane, therefore it is as if all the z-components were assumed to be
  zero. The use of a two-dimensional vector is mandatory (Attempting to use a
  3D vector would trigger an error... with one exception, which we will explore
  later in the discussion of splines).</p><p>
   Once the lines are determined, the ray-tracer rotates this line around the
  y-axis, and we can imagine a trail being left through space as it goes, with
  the surface of that trail being the surface of our object.</p><p>
   The specified points are connected with straight lines because we used the
  <code>linear_spline</code> keyword. There are other types of splines
  available with the lathe, which will result in smooth curving lines, and even
  rounded curving points of transition, but we will get back to that in a
  moment.</p><p>
   First, we would like to digress a moment to talk about the difference
  between a lathe and a surface of revolution object (SOR). The SOR object,
  described in a separate tutorial, may seem terribly similar to the lathe at
  first glance. It too declares a series of points and connects them with
  curving lines and then rotates them around the y-axis. The lathe has certain
  advantages, such as different kinds of splines, linear, quadratic and cubic,
  and one more thing:</p><p>
   The simpler mathematics used by a SOR doesn't allow the curve to double
  back over the same y-coordinates, thus, if using a SOR, any sudden twist
  which cuts back down over the same heights that the curve previously covered
  will trigger an error. For example, suppose we wanted a lathe to arc up from
  &lt;0,0&gt; to &lt;2,2&gt;, then to dip back down to &lt;4,0&gt;. Rotated
  around the y-axis, this would produce something like a gelatin mold - a
  rounded semi torus, hollow in the middle. But with the SOR, as soon as the
  curve doubled back on itself in the y-direction, it would become an illegal
  declaration.</p><p>
   Still, the SOR has one powerful strong point: because it uses simpler order
  mathematics, it generally tends to render faster than an equivalent lathe. So
  in the end, its a matter of: we use a SOR if its limitations will allow, but
  when we need a more flexible shape, we go with the lathe instead.</p>

</div>

<div class="divh4">
<a name="target_113"></a>
<h4>3.4.1.1&nbsp;&nbsp;Understanding The Concept of Splines</h4>

  <p>It would be helpful, in order to understand splines, if we had a sort of
  <em>Spline Workshop</em> where we could practice manipulating types and
  points of splines and see what the effects were like. So let's make one!
  Now that we know how to create a basic lathe, it will be easy:</p>
<pre>
#include &quot;colors.inc&quot;
  camera {
    orthographic
    up &lt;0, 5, 0&gt;
    right &lt;5, 0, 0&gt;
    location &lt;2.5, 2.5, -100&gt;
    look_at &lt;2.5, 2.5, 0&gt;
  }
  /* set the control points to be used */
  #declare Red_Point    = &lt;1.00, 0.00&gt;;
  #declare Orange_Point = &lt;1.75, 1.00&gt;;
  #declare Yellow_Point = &lt;2.50, 2.00&gt;;
  #declare Green_Point  = &lt;2.00, 3.00&gt;;
  #declare Blue_Point   = &lt;1.50, 4.00&gt;;
  /* make the control points visible */
  cylinder { Red_Point, Red_Point - &lt;0,0,20&gt;, .1
    pigment { Red }
    finish { ambient 1 }
  }
  cylinder { Orange_Point, Orange_Point - &lt;0,0,20&gt;, .1
    pigment { Orange }
    finish { ambient 1 }
  }
  cylinder { Yellow_Point, Yellow_Point - &lt;0,0,20&gt;, .1
    pigment { Yellow }
    finish { ambient 1 }
  }
  cylinder { Green_Point, Green_Point - &lt;0,0,20&gt;, .1
    pigment { Green }
    finish { ambient 1 }
  }
  cylinder { Blue_Point, Blue_Point- &lt;0,0,20&gt;, .1
    pigment { Blue }
    finish { ambient 1 }
  }
  /* something to make the curve show up */
  lathe {
    linear_spline
    5,
    Red_Point,
    Orange_Point,
    Yellow_Point,
    Green_Point,
    Blue_Point
    pigment { White }
    finish { ambient 1 }
  }
</pre>

  
<center><img src="images/spline.gif" alt="A simple &quot;Spline Workshop&quot;"><br><font size=-1><cite>A simple &quot;Spline Workshop&quot;</cite></font></center>


  <p>Now, we take a deep breath. We know that all looks a bit weird, but with
  some simple explanations, we can easily see what all this does.</p><p>
   First, we are using the orthographic camera. If we haven't read up on
  that yet, a quick summary is: it renders the scene <em>flat</em>, eliminating
  perspective distortion so that in a side view, the objects look like they
  were drawn on a piece of graph paper (like in the side view of a modeler or
  CAD package). There are several uses for this practical new type of camera,
  but here it is allowing us to see our lathe and cylinders<em> edge on</em>,
  so that what we see is almost like a cross section of the curve which makes
  the lathe, rather than the lathe itself. To further that effect, we
  eliminated shadowing with the <code>ambient 1</code> finish, which of course
  also eliminates the need for lighting. We have also positioned this
  particular side view so that &lt;0,0&gt; appears at the lower left of our
  scene.</p><p>
   Next, we declared a set of points. We note that we used 3D vectors for these
  points rather than the 2D vectors we expect in a lathe. That's the
  exception we mentioned earlier. When we declare a 3D point, then use it in a
  lathe, the lathe only uses the first two components of the vector, and
  whatever is in the third component is simply ignored. This is handy here,
  since it makes this example possible.</p><p>
   Next we do two things with the declared points. First we use them to place
  small diameter cylinders at the locations of the points with the circular
  caps facing the camera. Then we re-use those same vectors to determine the
  lathe.</p><p>
   Since trying to declare a 2D vector can have some odd results, and isn't
  really what our cylinder declarations need anyway, we can take advantage of
  the lathe's tendency to ignore the third component by just setting the
  z-coordinate in these 3D vectors to zero.</p><p>
   The end result is: when we render this code, we see a white lathe against a
  black background showing us how the curve we've declared looks, and the
  circular ends of the cylinders show us where along the x-y-plane our control
  points are. In this case, it's very simple. The linear spline has been
  used so our curve is just straight lines zig-zagging between the points. We
  change the declarations of <code>Red_Point</code> and <code>Blue_Point</code>
  to read as follows.</p>
<pre>
  #declare Red_Point  = &lt;2.00, 0.00&gt;;
  #declare Blue_Point = &lt;0.00, 4.00&gt;;
</pre>

  
<center><img src="images/mvspline.gif" alt="Moving some points of the spline"><br><font size=-1><cite>Moving some points of the spline</cite></font></center>


  <p>We re-render and, as we can see, all that happens is that the straight
  line segments just move to accommodate the new position of the red and blue
  points. Linear splines are so simple, we could manipulate them in our sleep,
  no?</p><p>
   Let's try something different. First, we change the points to the
  following.</p>
<pre>
  #declare Red_Point    = &lt;1.00, 0.00&gt;;
  #declare Orange_Point = &lt;2.00, 1.00&gt;;
  #declare Yellow_Point = &lt;3.50, 2.00&gt;;
  #declare Green_Point  = &lt;2.00, 3.00&gt;;
  #declare Blue_Point   = &lt;1.50, 4.00&gt;;
</pre>

  
<center><img src="images/quspline.gif" alt="A quadratic spline lathe"><br><font size=-1><cite>A quadratic spline lathe</cite></font></center>


  <p>We then go down to the lathe declaration and change <code>linear_spline</code>
  to <code>quadratic_spline</code>. We re-render and what do we have? Well,
  there's a couple of things worthy of note this time. First, we will see
  that instead of straight lines we have smooth arcs connecting the points.
  These arcs are made from quadratic curves, so our lathe looks much more
  interesting this time. Also, <code>Red_Point</code> is no longer connected
  to the curve. What happened?</p><p>
   Well, while any two points can determine a straight line, it takes three to
  determine a quadratic curve. POV-Ray looks not only to the two points to be
  connected, but to the point immediately preceding them to determine the
  formula of the quadratic curve that will be used to connect them. The problem
  comes in at the beginning of the curve. Beyond the first point in the curve
  there is no <em>previous</em> point. So we need to declare one. Therefore,
  when using a quadratic spline, we must remember that the first point we
  specify is only there so that POV-Ray can determine what curve to connect the
  first two points with. It will not show up as part of the actual curve.</p><p>
   There's just one more thing about this lathe example. Even though our
  curve is now put together with smooth curving lines, the transitions between
  those lines is... well, kind of choppy, no? This curve looks like the lines
  between each individual point have been terribly mismatched. Depending on
  what we are trying to make, this could be acceptable, or, we might long for a
  more smoothly curving shape. Fortunately, if the latter is true, we have
  another option.</p><p>
   The quadratic spline takes longer to render than a linear spline. The math
  is more complex. Still longer needs the cubic spline, yet, for a really
  smoothed out shape, this is the only way to go. We go back into our example,
  and simply replace <code>quadratic_spline</code> with <code>
  cubic_spline</code>. We render one more
  time, and take a look at what we have.</p>

  
<center><img src="images/cuspline.gif" alt="A cubic spline lathe"><br><font size=-1><cite>A cubic spline lathe</cite></font></center>


  <p> While a quadratic spline takes three points to determine the curve, a
  cubic needs four. So, as we might expect, <code>Blue_Point</code> has now
  dropped out of the curve, just as <code>Red_Point</code> did, as the first
  and last points of our curve are now only control points for shaping the
  curves between the remaining points. But look at the transition from <code>
  Orange_Point</code> to <code>Yellow_Point</code> and then back to <code>
  Green_Point</code>. Now, rather than looking mismatched, our curve segments
  look like one smoothly joined curve.</p>

<p> Finaly there is another kind of quadratic spline, the <code>bezier_spline</code>. 
This one takes four points per section. The start point, the end points and inbetween, 
two control points. To use it, we will have to make a few changes to our work shop. 
Delete the Yellow point, delete the Yellow cylinder. Change the points to:</p>
<pre>
  #declare Red_Point    = &lt;2.00, 1.00&gt;;
  #declare Orange_Point = &lt;3.00, 1.50&gt;;
  #declare Green_Point  = &lt;3.00, 3.50&gt;;
  #declare Blue_Point   = &lt;2.00, 4.00&gt;;
</pre>
<p>And change the lathe to:</p>
<pre>
  lathe {
    bezier_spline
    4,
    Red_Point,
    Orange_Point,
    Green_Point,
    Blue_Point
    pigment { White }
    finish { ambient 1 }
  }
</pre>
<p> The, green and orange, control points are not connected to the curve. Move them around a bit,
 for example <code>#declare Orange_Point = &lt;1.00, 1.50&gt;;</code>. The line that can be drawn
 from the start point to its closest control point (red to orange) shows the tangent of the curve
 at the start point. Same for the end point, blue to green.</p>
  
<center><img src="images/bezspline1.gif" alt="a bezier_spline lathe"><br><font size=-1><cite>a bezier_spline lathe</cite></font></center>

<p> One spline segment is nice, two is nicer. So we will add another segment and connect it to the
 blue point. One segment has four points, so two segments have eight. The first point of the second
 segment is the same as the last point of the first segment. The blue point. So we only have to
 declare three more points. Also we have to move the camera a bit and add more cylinders. Here is
 the complete scene again:</p>
<pre>
#include &quot;colors.inc&quot;
  camera {
    orthographic
    up &lt;0, 7, 0&gt;
    right &lt;7, 0, 0&gt;
    location &lt;3.5, 4, -100&gt;
    look_at &lt;3.5, 4, 0&gt;
  }
  /* set the control points to be used */
  #declare Red_Point    = &lt;2.00, 1.00&gt;;
  #declare Orange_Point = &lt;1.00, 1.50&gt;;
  #declare Green_Point  = &lt;3.00, 3.50&gt;;
  #declare Blue_Point   = &lt;2.00, 4.00&gt;;
  #declare Green_Point2 = &lt;3.00, 4.50&gt;;
  #declare Orange_Point2= &lt;1.00, 6.50&gt;;
  #declare Red_Point2   = &lt;2.00, 7.00&gt;;
  /* make the control points visible */

  cylinder { Red_Point, Red_Point - &lt;0,0,20&gt;, .1
    pigment { Red } finish { ambient 1 }
  }
  cylinder { Orange_Point, Orange_Point - &lt;0,0,20&gt;, .1
    pigment { Orange } finish { ambient 1 }
  }
  cylinder { Green_Point, Green_Point - &lt;0,0,20&gt;, .1
    pigment { Green } finish { ambient 1 }
  }
  cylinder { Blue_Point, Blue_Point- &lt;0,0,20&gt;, .1
    pigment { Blue } finish { ambient 1 }
  }
  cylinder { Green_Point2, Green_Point2 - &lt;0,0,20&gt;, .1
    pigment { Green } finish { ambient 1 }
  }
  cylinder { Orange_Point2, Orange_Point2 - &lt;0,0,20&gt;, .1
    pigment { Orange } finish { ambient 1 }
  }
  cylinder { Red_Point2, Red_Point2 - &lt;0,0,20&gt;, .1
    pigment { Red } finish { ambient 1 }
  }  
  /* something to make the curve show up */
  lathe {
    bezier_spline
    8,
    Red_Point, Orange_Point, Green_Point, Blue_Point
    Blue_Point, Green_Point2, Orange_Point2, Red_Point2
    pigment { White }
    finish { ambient 1 }
  }
</pre>

  
<center><img src="images/bezspline2.gif" alt="two bezier_spline segments, not smooth"><br><font size=-1><cite>two bezier_spline segments, not smooth</cite></font></center>


<p>
A nice curve, but what if we want a smooth curve? Let us have a look at the tangents
 on the Blue_point, draw the lines Green_Point, Blue_point and Green_Point2, Blue_point.
 Look at the angle they make, it is as sharp as the dent in the curve. What if we make
 the angle bigger? What if we make the angle 180°? Try a few positions for Green_point2
 and end with <code>#declare Green_Point2 = &lt;1.00, 4.50&gt;;</code>. A smooth curve.
 If we make sure that the two control points and the connection point are on one line,
 the curve is perfectly smooth. In general this can be achieved by 
<code>#declare Green_Point2 = Blue_Point+(Blue_Point-Green_Point);</code></p>

  
<center><img src="images/bezspline3.gif" alt="smooth bezier_spline lathe"><br><font size=-1><cite>smooth bezier_spline lathe</cite></font></center>


  <p> The concept of splines is a handy and necessary one, which will be seen
  again in the prism and polygon objects. But with a little tinkering we can
  quickly get a feel for working with them.</p>

</div>

<p>
<center>
<table width=100% border=0>
<tr>
<td>
<table width=100% align="center" border=0 bgcolor="#ffcc99" class="NavBar">
<tr>
<td align="left">&lt;<a href="povdoc_068.html">Prev</a></td>
<script language="JavaScript">
<!--
if (navigator.userAgent.indexOf ("MSIE 3.") >= 1)
  document.write ("<td align=center><small><i>You are running <b>IE3</b>. This will likely cause problems with this help file.</i></small></td>") ;
else if (navigator.userAgent.indexOf ("MSIE 4.") >= 1)
  document.write ("<td align=center><small><i>The Microsoft HTML Help system works best with <b>IE5</b> or later. We recommend you upgrade.</i></small></td>") ;
//-->
</script>
<td align="right"><a href="povdoc_070.html">Next</a>&gt;</td></tr>
</table>
</td>
</tr>
</table>
</center>
</body>
</html>
